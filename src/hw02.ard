\import Arith.Nat
\import Data.Bool
\import Data.List
\import Data.Or
\import Logic
\import Meta

-- 1. Определите полиморфный tail для списков.

\func tail {A : \Type} (l : List A) : List A \elim l
  | nil => nil
  | (x :: xs) => xs

-- 2. Реализуйте любой (but choose wisely) алгоритм сортировки. Функцию сравнения можно принимать в качестве аргумента.

\func sort {A : \Type} (l : List A) (less : A -> A -> Bool) : List A
  => insert-sort-acc l nil less
  \where {
    \func insert-sort-acc {A : \Type} (l acc : List A) (less : A -> A -> Bool) : List A \elim l
      | (x :: xs) => insert-sort-acc xs (insert x acc less) less
      | nil => acc
    \func insert {A : \Type} (a : A) (l : List A) (less : A -> A -> Bool) : List A \elim l
      | (x :: xs) => \case less a x \with {
        | false => x :: insert a xs less
        | true => a :: x :: xs
      }
      | nil => a :: nil
  }

\func \infix 4 < (x y : Nat) : Bool
  | 0, 0 => false
  | 0, suc y => true
  | suc x, 0 => false
  | suc x, suc y => x < y


\func l1 : List Nat => 1 :: 3 :: 2 :: nil

\func sort1 : List Nat => sort l1 (<)

-- 3. Докажите ассоциативность && и ||.

\func &&-assoc (x y z : Bool) : (x and y) and z = x and (y and z) \elim x, y
  | false, false => idp
  | false, true => idp
  | true, false => idp
  | true, true => idp

\func ||-assoc (x y z : Bool) : (x or y) or z = x or (y or z) \elim x, y
  | false, false => idp
  | false, true => idp
  | true, false => idp
  | true, true => idp

-- 4. Докажите, что fac 3 равно 6.
\open Nat

\func fac (x : Nat) : Nat
  | 0 => 1
  | suc x => suc x * fac x

\func fac3=4 : fac 3 = 6 => idp

-- 5. Докажите следующие пропозициональные тавтологии (choose 2 out of 3).

-- t1 : (P and Q -> R) -> P -> Q -> R
\func t1 {P Q R : \Type} (f : \Sigma P Q -> R) (p : P) (q : Q) : R => f (p, q)

-- t2 : (P -> R) -> (Q -> R) -> P or Q -> R
\func t2 {P Q R : \Type} (f : P -> R) (g : Q -> R) (h : Or P Q) : R \elim h
  | inl p => f p
  | inr q => g q

-- t3 : ((P -> Q -> R) -> P) -> (P -> R) -> R
\func t3 {P Q R : \Type} (f : (P -> Q -> R) -> P) (g : P -> R) : R => g (f (\lam p q => g p))

-- 6. Сформулируйте и докажите следующие пропозициональные тавтологии (choose 2 out of 3).

-- t4 : (P -> Q -> R) -> P and Q -> R
\func t4 {P Q R : \Type} (f : P -> Q -> R) (pq : \Sigma P Q) : R => f pq.1 pq.2
-- t5 : (P or Q -> P and Q) -> (P -> Q) and (Q -> P)
\func t5 {P Q R : \Type} (f : P `Or` Q -> \Sigma P Q) : \Sigma (P -> Q) (Q -> P)
  => (\lam p => (f (inl p)).2, \lam q => (f (inr q)).1)
-- t6 : ((((P -> Q) -> P) -> P) -> Q) -> Q

-- 7. Докажите следующие утверждения.

-- s1 : Если для любого x : Nat верно P(x), то существует x : Nat для которого P(x) верно.
\func s1 (P : Nat -> \Type) (h : \Pi (x : Nat) -> P x) : \Sigma (x : Nat) (P x) => (0, h 0)

-- s2 : Если не существует x : Nat такого, что верно P(x), то P(3) не верно.
\func s2 (P : Nat -> \Type) (h : \Sigma (x : Nat) (P x) -> Empty) : P 3 -> Empty
  => \lam p3 => h (3, p3)

-- 8. Сформулируйте и докажите следующие утверждения.

-- s3 : Если для всех x : Nat верно, что P(x) влечет Q(x), и существует x : Nat для которого верно P(x), то существует x : Nat для которого верно Q(x).

\func s3 (P : Nat -> \Type) (Q : Nat -> \Type) (h : \Pi (x : Nat) -> P x -> Q x) (g : \Sigma (x : Nat) (P x)) : \Sigma (x : Nat) (Q x)
  => (g.1, h g.1 g.2)


-- s4 : Если для всех x : Nat либо не верно P(x), либо не верно Q(x), то P(3) влечет, что Q(3) не верно.
\func s4 (P : Nat -> \Type) (Q : Nat -> \Type) (h : \Pi (x : Nat) -> Or (P x -> Empty) (Q x -> Empty)) : P 3 -> Q 3 -> Empty
  => \lam p3 q3 => \case h 3 \with {
    | inl px => px p3
    | inr qx => qx q3
  }
